// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture

RWTexture2D<float> FurthestVisibleDistances;
Texture2D<float> DepthMap;
RWTexture2D<float4> MainViewMask;

int2 RayOriginPixels;
float2 RayDirection;
float ViewAngle;
int NumRaysPerDegree;

int RayTextureSize;
int VisibilityMaskWidth;
int VisibilityMaskHeight;
float MinAlpha;

float RayOriginHeight;
float HighlightAngle;
float4x4 InvOrthoMatrix;
float3 CameraPosition;
float3 HighlightCenter;

int MaximumVisibleDistancePixels;

float2x2 rotate(float angle)
{
    return float2x2(
        cos(angle), -sin(angle),
        sin(angle), cos(angle));
}

void ProcessRay(uint3 id, inout float4 maskValue, float depth)
{
    float2 uv = float2(id.xy) / int2(VisibilityMaskWidth, VisibilityMaskHeight);
    uv = uv * 2.0f - 1.0f;
    float4 worldPos = mul(InvOrthoMatrix, float4(uv, DepthMap[id.xy], 1.0f));
    worldPos /= worldPos.w;
    
    float2 toPixel = (int2) id.xy - RayOriginPixels;
    float distance = length(toPixel.xy);
    
    uint numRays = ceil(ViewAngle * NumRaysPerDegree);
    
    if (RayOriginHeight > depth)
    {
        float t = dot(normalize(worldPos.xyz - CameraPosition), normalize(HighlightCenter - CameraPosition));
        if (t > HighlightAngle)
        {
            maskValue.z = 1 - (t - HighlightAngle) / HighlightAngle;
        }

    }
    
    
    float halfViewAngle = radians(ViewAngle / 2.0f);
    float angleStep = radians(ViewAngle) / (float) (numRays - 1);
    
    float2 normalizedToPixel = normalize(toPixel.xy);
    
    if (acos(dot(normalizedToPixel, RayDirection)) > halfViewAngle)
    {
        return;
    }
    float2 normalizedRayDirection = mul(rotate(-halfViewAngle), RayDirection);
    normalizedRayDirection = normalize(normalizedRayDirection);

    float dotProduct = dot(normalizedToPixel, normalizedRayDirection);
    float angle = acos(dotProduct);
    
    uint rayId = uint(round(angle / angleStep));
    if (rayId > numRays - 1)
    {
        return;
    }
    uint2 rayTextureCoord = uint2(rayId % (uint) RayTextureSize, rayId / (uint) RayTextureSize);
    float furthestVisibleDistance = FurthestVisibleDistances[rayTextureCoord].r;
    
    if (distance > furthestVisibleDistance)
    {
        return;
    }
    maskValue.y = lerp(0.1f, 1.0f,
        (
            dot(normalizedToPixel, RayDirection) - cos(halfViewAngle)
        ) / ( // -------------------------------
                    1.0f - cos(halfViewAngle)
        ) / MaximumVisibleDistancePixels * (MaximumVisibleDistancePixels - distance)
    );
    if (maskValue.x < 1.0f)
    {
        maskValue.x += maskValue.y / 32;
        if (maskValue.x > 1.0)
        {
            maskValue.x = 1.0;
        }
        return;
    }
}

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    float4 maskValue = MainViewMask[id.xy].r;
    float depth = DepthMap[id.xy];
    maskValue.yzw = 0.0f;
    ProcessRay(id, maskValue, depth);
    if (!(maskValue.y > 0 || maskValue.z > 0))
    {
        maskValue.x = max(min(maskValue.x, 0.1f), maskValue.x - 0.005f);
    }
    MainViewMask[id.xy] = maskValue;
}